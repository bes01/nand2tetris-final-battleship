class AIShips{
	
	field RandomUtil random;
	field Int xStart, yStart, dimension;
	field Array grid; // i - x, j - y
	field Int shipsLeft;
	field PlayerShips opponent;
	field Int xShot, yShot, xShotOrigin, yShotOrigin, shotDirection; // -1 - ship part not found, 0 - horizontal, 1 - vertical, 3 - unknown

	constructor AIShips new(RandomUtil randomInstance, Int x, Int y, Int shipDimension, PlayerShips player){
		let random = randomInstance;
		let xStart = x;
		let yStart = y;
		let dimension = shipDimension;
		let grid = Array.new(dimension);
		let shipsLeft = 7;
		let opponent = player;
		let shotDirection = -1;

		do resetBoard(true);

		return this;
	}

	// 0 - missed shot, 1 - ship is partially sank, 2 - fully sank, 3 - already marked, 4 - AI lost the game
	method Int receiveOpponentShot(int i, int j){
		var Array tempArray;
		var Int cellType;

		let tempArray = grid[i];
		let cellType = tempArray[j]; 

		if(cellType = 0){
			do toggleShipPart(i, j, 3);
			return 0;
		}
		if(cellType = 1){
			do toggleShipPart(i, j, 2);
			if(checkIfSank(i, j)){
				let shipsLeft = shipsLeft - 1;
				if(shipsLeft = 0){
					return 4;
				}
				return 2;
			}
			return 1;
		}

		return 3;
	}

	method Boolean checkIfSank(int i, int j){
		var Int start, end, direction;
		var Boolean exit, isSank;
		var Array tempArray;

		let direction = checkShipDirection(i, j);

		// horizontal
		if(direction = 1){
			// check left
			let exit = false;
			let isSank = true;
			let end = i;
			while((i > - 1) & ~exit){
				let tempArray = grid[i];
				if((tempArray[j] = 0) | (tempArray[j] = 3)){
					let exit = true;
				}
				if(~exit & checkUnsankShipPart(i, j)){
					let isSank = false;
					let exit = true;
				}
				if(~exit){
					let start = i;
				}
				let i = i - 1;
			}
			if(isSank){
				let exit = false;
				let i = end;
			}
			// check right
			while((i < 10) & ~exit){
				let tempArray = grid[i];
				if((tempArray[j] = 0) | (tempArray[j] = 3)){
					let exit = true;
				}
				if(~exit & checkUnsankShipPart(i, j)){
					let isSank = false;
					let exit = true;
				}
				if(~exit){
					let end = i;
				}
				let i = i + 1;
			}
			if(isSank){
				while(start < (end + 1)){
					do Sys.wait(200);
					do toggleShipPart(start, j, 1);
					let start = start + 1;
				}
				return true;
			}
		}
		
		// vertical
		if(direction = 2){
			let exit = false;
			let isSank = true;
			let end = j;
			while((j > - 1) & ~exit){
				let tempArray = grid[i];
				if((tempArray[j] = 0) | (tempArray[j] = 3)){
					let exit = true;
				}
				if(~exit & checkUnsankShipPart(i, j)){
					let isSank = false;
					let exit = true;
				}
				if(~exit){
					let start = j;
				}
				let j = j - 1;
			}
			if(isSank){
				let exit = false;
				let j = end;
			}
			// check right
			while((j < 10) & ~exit){
				let tempArray = grid[i];
				if((tempArray[j] = 0) | (tempArray[j] = 3)){
					let exit = true;
				}
				if(~exit & checkUnsankShipPart(i, j)){
					let isSank = false;
					let exit = true;
				}
				if(~exit){
					let end = j;
				}
				let j = j + 1;
			}
			if(isSank){
				while(start < (end + 1)){
					do Sys.wait(200);
					do toggleShipPart(i, start, 1);
					let start = start + 1;
				}
				return true;
			}
		}

		// point
		if(direction = 3){
			do Sys.wait(200);
			do toggleShipPart(i, j, 1);
			return true;
		}

		return false;
	}

	method Boolean checkUnsankShipPart(Int i, Int j){
		var Int x, y;
		var Array tempArray;

		let x = -1;
		while(x < 2){
			let y = -1;
			while(y < 2){
				if(validateBoundaries(i + x, j + y)){
					let tempArray = grid[i + (x)];
					if(tempArray[j + (y)] = 1){
						return true;
					}
				}
				let y = y + 1;
			}
			let x = x + 1;
		}

		return false;
	}

	// 0 - empty cell, 1 - horizontal, 2 - vertical, 3 - point
	method Int checkShipDirection(int i, int j){
		var Int x, y;
		var Array tempArray;
		var boolean isPoint;

		let isPoint = false;
		let tempArray = grid[i];
		if(tempArray[j] = 2){
			let isPoint = true;
		}

		// check horizontal
		if(isPoint & validateBoundaries(i + 1, j)){
			let tempArray = grid[i + 1];
			if((tempArray[j] = 2) | (tempArray[j] = 1)){
				return 1;
			}
		}
		if(isPoint & validateBoundaries(i - 1, j)){
			let tempArray = grid[i - 1];
			if((tempArray[j] = 2) | (tempArray[j] = 1)){
				return 1;
			}
		}

		// check vertical
		if(isPoint & validateBoundaries(i, j - 1)){
			let tempArray = grid[i];
			if((tempArray[j - 1] = 2) | (tempArray[j - 1] = 1)){
				return 2;
			}
		}
		if(isPoint & validateBoundaries(i, j + 1)){
			let tempArray = grid[i];
			if((tempArray[j + 1] = 2) | (tempArray[j + 1] = 1)){
				return 2;
			}
		}
		
		// isPoint
		if(isPoint){
			return 3;
		}

		// it's point
		return 0;
	}

	// modes: 0 - clear, 1 - X, 2 - !, 3 - #, 4 - mark matrix
	method void toggleShipPart(Int i, Int j, Int mode){
		var Array tempArray;
		var Int tempInt;

		let tempArray = grid[i];

		if(mode = 0){ // clear
			let tempArray[j] = 0;
			let i = xStart + (i * dimension);
			let j = yStart + (j * dimension);
			do Screen.setColor(false);
			do Screen.drawRectangle(i + 2, j + 2, i + dimension - 2, j + dimension - 2);
		}

		if(mode = 1){ // X - sank
			do toggleShipPart(i, j, 0);
			let tempArray[j] = -1;
			let i = xStart + (i * dimension);
			let j = yStart + (j * dimension);

			// drawing X
			do Screen.setColor(true);
			do Screen.drawLine(i + 2, j + 3, i + dimension - 2, j + dimension - 2);
			do Screen.drawLine(i + 2, j + 2, i + dimension - 3, j + dimension - 2);
			do Screen.drawLine(i + 2, j + dimension - 3, i + dimension - 3, j + 2);
			do Screen.drawLine(i + 2, j + dimension - 2, i + dimension - 2, j + 2);
			// do Screen.drawRectangle(i + 9, j + 15, i + 10, j + 17);
		}

		if(mode = 2){ // ! - partially sank
			let tempArray[j] = 2;
			let i = xStart + (i * dimension);
			let j = yStart + (j * dimension);

			// drawing !
			do Screen.setColor(true);
			do Screen.drawRectangle(i + 9, j + 2, i + 10, j + 13);
			do Screen.drawRectangle(i + 9, j + 15, i + 10, j + 17);
		}

		if(mode = 3){ // # - missed shots
			let tempArray[j] = 3;
			let i = xStart + (i * dimension);
			let j = yStart + (j * dimension);
			let tempInt = ((dimension / 3) + 1);

			// drawing #
			do Screen.drawLine(i + 1, j + tempInt, i + 18, j + tempInt);
			do Screen.drawLine(i + 1, j + (2 * tempInt), i + 18, j + (2 * tempInt));
			do Screen.drawLine(i + tempInt, j + 1, i + tempInt, j + 18);
			do Screen.drawLine(i + (2 * tempInt), j + 1, i + (2 * tempInt), j + 18);
		}

		if(mode = 4){ // mark matrix / put ship
			let tempArray[j] = 1;
			let i = xStart + (i * dimension);
			let j = yStart + (j * dimension);
		}

		return;
	}

	// find place randomly for ship
	method void placeShipSomewhere(Int shipLength){
		var Int start, end;
		var Int i;
		var Int j;
		var Boolean direction; // false - vertical, true - horizontal
		var Boolean endLoop;
		var Int k;

		
		while(true){ // keep while until success
			let i = random.nextInt(10);
			let j = random.nextInt(10);

			if(validateCoordinate(i, j)){
				if(shipLength = 1){
					do toggleShipPart(i, j, 4);
					return;
				}

				let direction = random.nextBoolean(); // TODO: make better random
				if(direction = 0){ // first we try to go up, then down
					let end = j;
					let endLoop = false;
					while((j > -1) & ~endLoop){ // find space up
						if(~validateCoordinate(i, j)){
							let endLoop = true;
							let start = j + 1;
						}
						let j = j - 1;
					}
					let j = end; // restore j;
					let endLoop = false;
					while((j < (dimension - 1)) & ~endLoop){
						if(~validateCoordinate(i, j)){
							let endLoop = true;
							let end = j - 1;
						}
						let j = j + 1;
					}

					if((((end - start) + 1) > shipLength) | (((end - start) + 1) = shipLength)){
						let k = 0;
						while(k < shipLength){
							do toggleShipPart(i, start + k, 4);
							let k = k + 1;
						}
						return;
					}									
				}
				if(direction = 1){ // first we try to go left, then right
					let end = i;
					let endLoop = false;
					while((i > -1) & ~endLoop){ // find left up
						if(~validateCoordinate(i, j)){
							let endLoop = true;
							let start = i + 1;
						}
						let i = i - 1;
					}
					let i = end; // restore i;
					let endLoop = false;
					while((i < (dimension - 1)) & ~endLoop){
						if(~validateCoordinate(i, j)){
							let endLoop = true;
							let end = i - 1;
						}
						let i = i + 1;
					}
					if((((end - start) + 1) > shipLength) | (((end - start) + 1) = shipLength)){
						let k = 0;
						
						while(k < shipLength){
							do toggleShipPart(start + k, j, 4);
							let k = k + 1;
						}
						return;
					}
				}
			}
		}

		return;
	}

	// validates that no adjacent ship presents
	method Boolean validateCoordinate(Int i, Int j){
		var Int x, y;
		var Array tempArray;

		let x = -1;
		while(x < 2){
			let y = -1;
			while(y < 2){
				if(validateBoundaries(i + x, j + y)){
					let tempArray = grid[i + (x)];
					if(tempArray[j + (y)]){
						return false;
					}
				}
				let y = y + 1;
			}
			let x = x + 1;
		}

		return true;
	}

	method boolean validateBoundaries(Int i, Int j){
		return (i > -1) & (i < 10) & (j > -1) & (j < 10); 
	}

	method void resetBoard(Boolean firstTime){
		var Int i, j;
		var Array tempArray;

		let i = 0;
		while(i < 10){
			let j = 0;
			if(firstTime){
				let tempArray = Array.new(10);
				let grid[i] = tempArray;
			}
			if(~firstTime){
				let tempArray = grid[i];				
			}
			while(j < 10){
				if(tempArray[j]){
					do toggleShipPart(i, j, 0);
				}
				let tempArray[j] = 0;
				let j = j + 1;
			}
			let i = i + 1;
		}

		// randomize ship locations
		do placeShipSomewhere(5);
		do placeShipSomewhere(4);
		do placeShipSomewhere(3);
		do placeShipSomewhere(2);
		do placeShipSomewhere(2);
		do placeShipSomewhere(1);
		do placeShipSomewhere(1);

		return;
	}

	// true - computer won the game
	method boolean playTurn(){
		var Boolean exit, directionChanged;
		var Int i, j, shotResult;
		let exit = false;

		while(~exit){
			let i = random.nextInt(10);
			let j = random.nextInt(10);

			if(shotDirection > -1){
				do guessShipPart();
				let i = xShot;
				let j = yShot;
			}		

			if(~opponent.alreadyShot(i, j)){
				do Sys.wait(200);
				let shotResult = opponent.receiveOpponentShot(i, j);
				let directionChanged = false;
				if(shotResult = 0){
					let exit = true;
				}
				if (shotResult = 1){ // imitate clever moves
					if(shotDirection = -1){ // first successful shot
						let xShotOrigin = i;
						let yShotOrigin = j;
						let shotDirection = 3;
						let directionChanged = true;
					}
					if((shotDirection = 3) & ~directionChanged){ // second shot gives us direction
						let shotDirection = 1; // horizontal
						if(xShot = xShotOrigin){ // guessed shot wasn't horizontal
							let shotDirection = 2; // vertical
						}
					}
				}
				if (shotResult = 2){ // ship fully sank
					let shotDirection = -1;
				}
				if (shotResult = 4){ // last ship fully sank and computer won the game
					return true;
				}
			}
		}

		return false;
	}

	method void guessShipPart(){
		var Boolean randomDirection, alreadyTried; // false - horizontal, true - vertical

		let alreadyTried = false;

		if(shotDirection = 3){
			let randomDirection = random.nextBoolean();
			let xShot = xShotOrigin;
			let yShot = yShotOrigin;
			if(randomDirection){ // vertical
				if((yShot > 0) & ~opponent.alreadyShot(xShot, yShot - 1)){ // try up
					let yShot = yShot - 1;
					return; 
				}				
				if((yShot < 9) & ~opponent.alreadyShot(xShot, yShot + 1)){ // try up
					let yShot = yShot + 1;
					return; 
				}		
				let alreadyTried = true;	
			}
			if((~randomDirection) | alreadyTried){ // horizontal
				if((xShot > 0) & ~opponent.alreadyShot(xShot - 1, yShot)){ // try up
					let xShot = xShot - 1;
					return; 
				}				
				if((xShot < 9) & ~opponent.alreadyShot(xShot + 1, yShot)){ // try up
					let xShot = xShot + 1;
					return; 
				}	
			}
			 // try vertical in case randomDirection was horizontal and didnt succeed
			if((yShot > 0) & ~opponent.alreadyShot(xShot, yShot - 1)){ // try up
				let yShot = yShot - 1;
				return; 
			}				
			if((yShot < 9) & ~opponent.alreadyShot(xShot, yShot + 1)){ // try up
				let yShot = yShot + 1;
				return; 
			}		
		}

		// Vertical
		if(shotDirection = 2){
			// TODO: traverse vertically
			return;
		}

		// Horizontal
		if(shotDirection = 1){
			// TODO: traverse horizontally
			return;
		}

		return;
	}

}